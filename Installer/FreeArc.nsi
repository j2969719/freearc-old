; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!ifndef XXX
!define PRODUCT_NAME "FreeArc"
!else
!define PRODUCT_NAME "Hamster"
!endif
!define PRODUCT_VERSION "0.666"
!define PRODUCT_PUBLISHER "Bulat Ziganshin"
!define PRODUCT_WEB_SITE "http://freearc.org"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\${PRODUCT_NAME}.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

SetCompressor /SOLID lzma

!include "setenv.nsh"
!include "Library.nsh"
!include "TextFunc.nsh"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_COMPONENTSPAGE_DESC
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"
ReserveFile "License\License.txt"

; Welcome page
!define MUI_WELCOMEPAGE_TEXT "${PRODUCT_NAME} is an archiver featuring:\n\n- best speed and compression ratio among all practical archivers\n- dedicated algorithms for multimedia, executable and text compression\n- compression dictionaries up to 2gb\n- solid archives with smart updating\n- AES+Twofish+Serpent encryption\n- recovery record\n- self-extracting archives\n- extraction of zip,rar,7z,gz and many other archive formats\n- integration with Explorer, Total Commander, FAR manager and InnoSetup\n- GUI and console versions both for Windows and Linux\n- free for commercial and non-commercial use"
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "License\License.txt"
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Components selection page
!insertmacro MUI_PAGE_COMPONENTS
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\bin\FreeArc.exe"
!define MUI_FINISHPAGE_SHOWREADME "$INSTDIR\Documentation\whatsnew.htm"
!define MUI_FINISHPAGE_SHOWREADME_TEXT "Show what's new in the latest versions"
;!define MUI_FINISHPAGE_SHOWREADME_NOTCHECKED
!define MUI_FINISHPAGE_LINK "Visit http://freearc.org"
!define MUI_FINISHPAGE_LINK_LOCATION "http://freearc.org"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; Reserve files
!insertmacro MUI_RESERVEFILE_INSTALLOPTIONS

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
!ifdef UPDATE
OutFile "FreeArc-update.exe"
!else
OutFile "FreeArc-install.exe"
!endif

InstallDir "$PROGRAMFILES\${PRODUCT_NAME}"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Section "Install ${PRODUCT_NAME}" Install_FreeArc
  !include "FreeArc-delete-old.nsh"   ; Delete files from older installations

  ;Backup config files
  SetOverwrite off
  CreateDirectory "$INSTDIR\bin\backup"
  Delete "$INSTDIR\bin\backup\freearc.ini"
  Delete "$INSTDIR\bin\backup\freearc.history"
  Delete "$INSTDIR\bin\backup\arc.groups"
  Delete "$INSTDIR\bin\backup\arc.ini"
  CopyFiles "$INSTDIR\bin\freearc.ini"      "$INSTDIR\bin\backup\freearc.ini"
  CopyFiles "$INSTDIR\bin\freearc.history"  "$INSTDIR\bin\backup\freearc.history"
  CopyFiles "$INSTDIR\bin\arc.groups"       "$INSTDIR\bin\backup\arc.groups"
  CopyFiles "$INSTDIR\bin\arc.ini"          "$INSTDIR\bin\backup\arc.ini"
  SetOutPath "$INSTDIR\bin"
  File /r "ini\*.*"
  SetOverwrite on

  SetOutPath "$INSTDIR"
  File    "FreeArc.url"
  SetOutPath "$INSTDIR\Addons"
  File /r "Addons\*.*"
  SetOutPath "$INSTDIR\bin"
  File /r "bin\*.*"
  SetOutPath "$INSTDIR\Documentation"
  File /r "Documentation\*.*"
  SetOutPath "$INSTDIR\License"
  File /r "License\*.*"
!ifndef UPDATE
  SetOutPath "$INSTDIR"
  File /r "GTK2-Runtime\*.*"
  SetOutPath "$INSTDIR"
  File /r "gtk2-themes\*.*"
!endif

  ; If ArcShellExt*.dll files are currently used, replace them after reboot
  !define LIBRARY_SHELL_EXTENSION
  !define LIBRARY_IGNORE_VERSION
  !insertmacro InstallLib DLL NOTSHARED REBOOT_NOTPROTECTED "ArcShellExt-dll\ArcShellExt.dll"    "$INSTDIR\bin\ArcShellExt\ArcShellExt.dll"    "$INSTDIR"
  !insertmacro InstallLib DLL NOTSHARED REBOOT_NOTPROTECTED "ArcShellExt-dll\ArcShellExt-64.dll" "$INSTDIR\bin\ArcShellExt\ArcShellExt-64.dll" "$INSTDIR"
SectionEnd

SubSection /e "Options" pOptions
  Section "Use personal settings" Use_personal_settings
  SectionEnd

  Section "Associate .arc files with ${PRODUCT_NAME}" Settings_Associate
  SectionEnd

  Section "Associate other archives with ${PRODUCT_NAME}" Settings_Associate_Zip
  SectionEnd

  Section "Add to Start Menu" Add_to_Start_Menu
    SetDetailsPrint textonly
    DetailPrint "Installing Start Menu shortcuts..."
    SetDetailsPrint listonly
    RMDir /r "$SMPROGRAMS\${PRODUCT_NAME}"
    CreateDirectory "$SMPROGRAMS\${PRODUCT_NAME}"
    CreateDirectory "$SMPROGRAMS\${PRODUCT_NAME}\Documentation"
    CreateShortCut  "$SMPROGRAMS\${PRODUCT_NAME}\Documentation\License.lnk" "$INSTDIR\License\License.txt"
    CreateShortCut  "$SMPROGRAMS\${PRODUCT_NAME}\Documentation\What's new.lnk" "$INSTDIR\Documentation\whatsnew.txt"
    CreateShortCut  "$SMPROGRAMS\${PRODUCT_NAME}\Documentation\${PRODUCT_NAME} GUI.lnk" "$INSTDIR\Documentation\FreeArc-GUI-Eng.htm"
    CreateShortCut  "$SMPROGRAMS\${PRODUCT_NAME}\Documentation\${PRODUCT_NAME} command line.lnk" "$INSTDIR\Documentation\FreeArc036-eng.htm"
    CreateDirectory "$SMPROGRAMS\${PRODUCT_NAME}\Documentation (rus)"
    CreateShortCut  "$SMPROGRAMS\${PRODUCT_NAME}\Documentation (rus)\What's new.lnk" "$INSTDIR\Documentation\whatsnew-rus.txt"
    CreateShortCut  "$SMPROGRAMS\${PRODUCT_NAME}\Documentation (rus)\${PRODUCT_NAME} GUI.lnk" "$INSTDIR\Documentation\FreeArc-GUI-Rus.htm"
    CreateShortCut  "$SMPROGRAMS\${PRODUCT_NAME}\Documentation (rus)\${PRODUCT_NAME} command line.lnk" "$INSTDIR\Documentation\FreeArc040-rus.htm"
    SetOutPath "$INSTDIR\bin"
    CreateShortCut  "$SMPROGRAMS\${PRODUCT_NAME}\${PRODUCT_NAME}.lnk"     "$INSTDIR\bin\FreeArc.exe"
    CreateShortCut  "$SMPROGRAMS\${PRODUCT_NAME}\Change skin.lnk" "$INSTDIR\bin\gtk2_prefs.exe"
    SetOutPath "$INSTDIR"
    CreateShortCut  "$SMPROGRAMS\${PRODUCT_NAME}\Website.lnk"     "$INSTDIR\FreeArc.url"
    CreateShortCut  "$SMPROGRAMS\${PRODUCT_NAME}\Uninstall.lnk"   "$INSTDIR\uninst.exe"
  SectionEnd

  Section "Add shortcut to desktop" Add_shortcut_to_desktop
    CreateShortCut  "$DESKTOP\${PRODUCT_NAME}.lnk" "$INSTDIR\bin\FreeArc.exe"
  SectionEnd

  Section "Add shortcut to Quick Launch menu" Add_shortcut_to_Quick_Launch_menu
    CreateShortCut  "$QUICKLAUNCH\${PRODUCT_NAME}.lnk" "$INSTDIR\bin\FreeArc.exe"
  SectionEnd

  Section "Add ${PRODUCT_NAME} directory to PATH" Add_FreeArc_to_PATH
    SetDetailsPrint textonly
    DetailPrint "Setting PATH variable..."
    SetDetailsPrint listonly
    Push "$INSTDIR\bin"
    Call AddToPath
  SectionEnd
SubSectionEnd

LangString DESC_Use_personal_settings              ${LANG_ENGLISH} "Store settings for every user in %APPDATA% directory: allows to use program with UAC and by non-administrator users"
LangString DESC_Settings_Associate                 ${LANG_ENGLISH} "Associate ${PRODUCT_NAME} with .arc files in Explorer and provide context menu"
LangString DESC_Settings_Associate_Zip             ${LANG_ENGLISH} "Associate ${PRODUCT_NAME} with other archive files in Explorer"
LangString DESC_Add_to_Start_Menu                  ${LANG_ENGLISH} "Add shortcuts to ${PRODUCT_NAME} program and documentation files to Windows Start menu"
LangString DESC_Add_shortcut_to_desktop            ${LANG_ENGLISH} "Add ${PRODUCT_NAME} shortcut to Windows desktop"
LangString DESC_Add_shortcut_to_Quick_Launch_menu  ${LANG_ENGLISH} "Add ${PRODUCT_NAME} shortcut to Windows Quick Launch menu"
LangString DESC_Add_FreeArc_to_PATH                ${LANG_ENGLISH} "Add ${PRODUCT_NAME} directory to PATH environment variable, allowing to run ${PRODUCT_NAME} from command prompt and batch files"

!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${Use_personal_settings}                  $(DESC_Use_personal_settings)
  !insertmacro MUI_DESCRIPTION_TEXT ${Settings_Associate}                     $(DESC_Settings_Associate)
  !insertmacro MUI_DESCRIPTION_TEXT ${Settings_Associate_Zip}                 $(DESC_Settings_Associate_Zip)
  !insertmacro MUI_DESCRIPTION_TEXT ${Add_to_Start_Menu}                      $(DESC_Add_to_Start_Menu)
  !insertmacro MUI_DESCRIPTION_TEXT ${Add_shortcut_to_desktop}                $(DESC_Add_shortcut_to_desktop)
  !insertmacro MUI_DESCRIPTION_TEXT ${Add_shortcut_to_Quick_Launch_menu}      $(DESC_Add_shortcut_to_Quick_Launch_menu)
  !insertmacro MUI_DESCRIPTION_TEXT ${Add_FreeArc_to_PATH}                    $(DESC_Add_FreeArc_to_PATH)
!insertmacro MUI_FUNCTION_DESCRIPTION_END


!macro DeletePersonalFiles
  Delete /REBOOTOK  "$APPDATA\${PRODUCT_NAME}\ArcShellExt\ArcShellExt-config.lua"
  Delete /REBOOTOK  "$APPDATA\${PRODUCT_NAME}\freearc.ini"
  RMDir  /REBOOTOK  "$APPDATA\${PRODUCT_NAME}\ArcShellExt"
  RMDir  /REBOOTOK  "$APPDATA\${PRODUCT_NAME}"
!macroend

Function .onVerifyInstDir
  IfFileExists $APPDATA\${PRODUCT_NAME}\freearc.ini 0 Global
    ${ConfigRead} "$APPDATA\${PRODUCT_NAME}\freearc.ini" "UsePersonalSettings=" $R0
    StrCmp $R0 "1" 0 NoUsePersonalSettings
      SectionSetFlags ${Use_personal_settings} ${SF_SELECTED}
      Goto CheckSettingsAssociate
    NoUsePersonalSettings:
      SectionSetFlags ${Use_personal_settings} 0

CheckSettingsAssociate:
    ${ConfigRead} "$APPDATA\${PRODUCT_NAME}\freearc.ini" "Settings.Associate=" $R0
    StrCmp $R0 "1" 0 NoSettingsAssociate
      SectionSetFlags ${Settings_Associate} ${SF_SELECTED}
      Goto CheckSettingsAssociateZip
    NoSettingsAssociate:
      SectionSetFlags ${Settings_Associate} 0

CheckSettingsAssociateZip:
    ${ConfigRead} "$APPDATA\${PRODUCT_NAME}\freearc.ini" "Settings.AssociateNonArc=" $R0
    StrCmp $R0 "1" 0 NoSettingsAssociateZip
      SectionSetFlags ${Settings_Associate_Zip} ${SF_SELECTED}
      Return
    NoSettingsAssociateZip:
      SectionSetFlags ${Settings_Associate_Zip} 0
      Return


Global:
  IfFileExists $INSTDIR\bin\freearc.ini 0 EmptyUsePersonalSettings2
    ${ConfigRead} "$INSTDIR\bin\freearc.ini" "UsePersonalSettings=" $R0
    StrCmp $R0 "1" 0 NoUsePersonalSettings2
      SectionSetFlags ${Use_personal_settings} ${SF_SELECTED}
      Goto CheckSettingsAssociate2
    NoUsePersonalSettings2:
      SectionSetFlags ${Use_personal_settings} 0
      Goto CheckSettingsAssociate2
  EmptyUsePersonalSettings2:
    SectionSetFlags ${Use_personal_settings} ${SF_SELECTED}

CheckSettingsAssociate2:
  IfFileExists $INSTDIR\bin\freearc.ini 0 EmptySettingsAssociate2
    ${ConfigRead} "$INSTDIR\bin\freearc.ini" "Settings.Associate=" $R0
    StrCmp $R0 "1" 0 NoSettingsAssociate2
      SectionSetFlags ${Settings_Associate} ${SF_SELECTED}
      Goto CheckSettingsAssociateZip2
    NoSettingsAssociate2:
      SectionSetFlags ${Settings_Associate} 0
      Goto CheckSettingsAssociateZip2
  EmptySettingsAssociate2:
    SectionSetFlags ${Settings_Associate} ${SF_SELECTED}

CheckSettingsAssociateZip2:
  IfFileExists $INSTDIR\bin\freearc.ini 0 EmptySettingsAssociateZip2
    ${ConfigRead} "$INSTDIR\bin\freearc.ini" "Settings.AssociateNonArc=" $R0
    StrCmp $R0 "1" 0 NoSettingsAssociateZip2
      SectionSetFlags ${Settings_Associate_Zip} ${SF_SELECTED}
      Return
    NoSettingsAssociateZip2:
      SectionSetFlags ${Settings_Associate_Zip} 0
      Return
  EmptySettingsAssociateZip2:
    SectionSetFlags ${Settings_Associate_Zip} 0
FunctionEnd

Section -AdditionalIcons
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\bin\FreeArc.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"

  Var /global config
  SectionGetFlags ${Use_personal_settings} $0
  IntOp  $0 $0 & ${SF_SELECTED}
  IntCmp $0 ${SF_SELECTED} UsePersonalSettings NoUsePersonalSettings
    UsePersonalSettings:
      ${ConfigWrite} "$INSTDIR\bin\freearc.ini" "UsePersonalSettings=" "1" $R0
      strcpy $config "$APPDATA\${PRODUCT_NAME}\freearc.ini"
      Goto AfterUsePersonalSettings
    NoUsePersonalSettings:
      ${ConfigWrite} "$INSTDIR\bin\freearc.ini" "UsePersonalSettings=" "0" $R0
      strcpy $config "$INSTDIR\bin\freearc.ini"
      !insertmacro DeletePersonalFiles
    AfterUsePersonalSettings:

  SectionGetFlags ${Settings_Associate} $0
  IntOp  $0 $0 & ${SF_SELECTED}
  IntCmp $0 ${SF_SELECTED} SettingsAssociate NoSettingsAssociate
    SettingsAssociate:
      ${ConfigWrite} $config "Settings.Associate=" "1" $R0
      ${ConfigWrite} $config "Settings.ContextMenu=" "1" $R0
      Goto AfterSettingsAssociate
    NoSettingsAssociate:
      ${ConfigWrite} $config "Settings.Associate=" "0" $R0
      ${ConfigWrite} $config "Settings.ContextMenu=" "0" $R0
    AfterSettingsAssociate:

  SectionGetFlags ${Settings_Associate_Zip} $0
  IntOp  $0 $0 & ${SF_SELECTED}
  IntCmp $0 ${SF_SELECTED} SettingsAssociateZip NoSettingsAssociateZip
    SettingsAssociateZip:
      ${ConfigWrite} $config "Settings.AssociateNonArc=" "1" $R0
      Goto AfterSettingsAssociateZip
    NoSettingsAssociateZip:
      ${ConfigWrite} $config "Settings.AssociateNonArc=" "0" $R0
    AfterSettingsAssociateZip:

;  SectionGetFlags ${Settings_Context_Menu} $0
;  IntOp  $0 $0 & ${SF_SELECTED}
;  IntCmp $0 ${SF_SELECTED} SettingsContextMenu NoSettingsContextMenu
;    SettingsContextMenu:
;      ${ConfigWrite} $config "Settings.ContextMenu=" "1" $R0
;      Goto AfterSettingsAssociateZip
;    NoSettingsContextMenu:
;      ${ConfigWrite} $config "Settings.ContextMenu=" "0" $R0
;    AfterSettingsContextMenu:

  ExecWait '"$INSTDIR\bin\FreeArc.exe" --register'
SectionEnd


Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  SetShellVarContext "all"
    ExecWait '"$INSTDIR\bin\FreeArc.exe" --unregister'
  SetShellVarContext "current"
    ExecWait '"$INSTDIR\bin\FreeArc.exe" --unregister'
  ExecWait '"$INSTDIR\PowerPack\unins000.exe" /VERYSILENT'
  RMDir /REBOOTOK   "$INSTDIR\PowerPack"

  SetShellVarContext "all"
    RMDir /r "$SMPROGRAMS\${PRODUCT_NAME}"
    Delete   "$DESKTOP\${PRODUCT_NAME}.lnk"
    Delete   "$QUICKLAUNCH\${PRODUCT_NAME}.lnk"
    Push "$INSTDIR\bin"
    Call un.RemoveFromPath
    Push "$INSTDIR"
    Call un.RemoveFromPath

  SetShellVarContext "current"
    RMDir /r "$SMPROGRAMS\${PRODUCT_NAME}"
    Delete   "$DESKTOP\${PRODUCT_NAME}.lnk"
    Delete   "$QUICKLAUNCH\${PRODUCT_NAME}.lnk"
    Push "$INSTDIR\bin"
    Call un.RemoveFromPath
    Push "$INSTDIR"
    Call un.RemoveFromPath

  !include "FreeArc-delete-old.nsh"   ; Delete files from previous FreeArc versions
  !include "FreeArc-delete-all.nsh"
  RMDir /REBOOTOK   "$INSTDIR"
  !insertmacro DeletePersonalFiles

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"

  SetAutoClose true
SectionEnd

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd
